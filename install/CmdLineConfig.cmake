
CMAKE_MINIMUM_REQUIRED( VERSION 3.12 )

SET( CMDLINE_KNOWN_COMPS "static" "shared" )
SET( CMDLINE_COMP_static NO )
SET( CMDLINE_COMP_shared NO )
FOREACH( CMDLINE_COMP IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS )
  IF( CMDLINE_COMP IN_LIST CMDLINE_KNOWN_COMPS )
    SET( CMDLINE_COMP_${CMDLINE_COMP} YES )
  else()
    SET( ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
         "Library does not recognize component `${CMDLINE_COMP}`." )
    SET( ${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE )
    RETURN()
  ENDIF()
ENDFOREACH ()

IF( CMDLINE_COMP_static AND CMDLINE_COMP_shared )
  SET( ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
       "The components `static` and `shared` are mutually exclusive." )
  SET( ${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE )
  RETURN()
ENDIF()

SET( CMDLINE_STATIC_TARGETS 
     "${CMAKE_CURRENT_LIST_DIR}/${CMAKE_FIND_PACKAGE_NAME}-static-targets.cmake" )
SET( CMDLINE_SHARED_TARGETS 
     "${CMAKE_CURRENT_LIST_DIR}/${CMAKE_FIND_PACKAGE_NAME}-shared-targets.cmake" )

MACRO( CMDLINE_LOAD_TARGETS ltype )
  IF( NOT EXISTS "${CMDLINE_${ltype}_TARGETS}" )
    SET( ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
         "The requested `${ltype}` libraries are not available." )
    SET( ${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE )
    RETURN()
  ENDIF()
  INCLUDE( "${CMDLINE_${ltype}_TARGETS}" )
ENDMACRO()

IF( CMDLINE_COMP_static )
  CMDLINE_LOAD_TARGETS( "STATIC" )
ELSEIF( CMDLINE_COMP_shared )
  CMDLINE_LOAD_TARGETS( "SHARED" )
ELSEIF( DEFINED CMDLINE_BUILD_SHARED_LIB AND CMDLINE_BUILD_SHARED_LIB )
  CMDLINE_LOAD_TARGETS( "SHARED" )
ELSEIF( DEFINED CMDLINE_BUILD_SHARED_LIB AND NOT CMDLINE_BUILD_SHARED_LIB )
  CMDLINE_LOAD_TARGETS( "STATIC" )
ELSEIF( BUILD_SHARED_LIBS )
  IF( EXISTS "${CMDLINE_SHARED_TARGETS}" )
    CMDLINE_LOAD_TARGETS( "SHARED" )
  ELSE()
    CMDLINE_LOAD_TARGETS( "STATIC" )
  ENDIF()
ELSE()
  IF( EXISTS "${CMDLINE_STATIC_TARGETS}" )
    CMDLINE_LOAD_TARGETS( "STATIC" )
  ELSE()
    CMDLINE_LOAD_TARGETS( "SHARED" )
  ENDIF()
ENDIF()
